version '1.0'

buildscript {
    ext.kotlinVersion = '1.3.30'

    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'kotlin'
apply plugin: 'docker'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    coroutinesVersion = '1.2.0'
    guiceVersion = '4.2.2'
    ktorVersion = '1.1.4'
    logbackClassicVersion = '1.2.3'
    typesafeConfigVersion = '1.3.3'

    junitVersion = '5.4.2'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    compile "com.google.inject:guice:$guiceVersion"
    compile "com.google.inject.extensions:guice-multibindings:$guiceVersion"
    compile "com.typesafe:config:$typesafeConfigVersion"
    compile "ch.qos.logback:logback-classic:$logbackClassicVersion"

    compile "io.ktor:ktor-jackson:$ktorVersion"
    compile "io.ktor:ktor-server-netty:$ktorVersion"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"

}

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
    test.java.srcDirs += "src/test/kotlin"
}

shadowJar() {
    archiveName = 'backend.jar'
    baseName = 'backend'
    mergeServiceFiles()
    exclude 'META-INF/.SF'
    exclude 'META-INF/.DSA'
    exclude 'META-INF/.RSA'
    manifest {
        attributes 'Main-Class': 'com.github.shaad.backend.StarterKt'
    }
}

build.dependsOn shadowJar
task buildDocker(type: Docker) {
    dependsOn build
    push = false
    applicationName = 'backend'
    tagVersion = 'latest'
    dockerfile = file('deploy/docker/Dockerfile')
    doFirst {
        copy {
            from 'build/libs/backend.jar'
            into stageDir
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

